;;;; ginjish-tests.lisp

(cl:defpackage #:ginjish-tests
  (:use #:cl #:parachute))

(cl:in-package #:ginjish-tests)

;;; parser

(define-test parser)

(define-test stringliteral
  :parent parser
  (is equal
      '(:string "abc")
      (esrap:parse 'ginjish-grammar::stringliteral "\"abc\""))
  (is equal
      '(:string "abc")
      (esrap:parse 'ginjish-grammar::stringliteral "'abc'")))

(define-test integer
  :parent parser
  (is =
      7
      (esrap:parse 'ginjish-grammar::integer "7"))
  (is =
      2147483647
      (esrap:parse 'ginjish-grammar::integer "2147483647"))
  (is =
      127
      (esrap:parse 'ginjish-grammar::integer "0o177"))
  (is =
      311
      (esrap:parse 'ginjish-grammar::integer "0b100110111"))
  (is =
      3
      (esrap:parse 'ginjish-grammar::integer "3"))
  (is =
      79228162514264337593543950336
      (esrap:parse 'ginjish-grammar::integer "79228162514264337593543950336"))
  (is =
      255
      (esrap:parse 'ginjish-grammar::integer "0o377"))
  (is =
      3735928559
      (esrap:parse 'ginjish-grammar::integer "0xdeadbeef"))
  (is =
      100000000000
      (esrap:parse 'ginjish-grammar::integer "100_000_000_000"))
  (is =
      229
      (esrap:parse 'ginjish-grammar::integer "0b_1110_0101")))

(define-test floatnumber
  :parent parser
  (is =
      3.14
      (esrap:parse 'ginjish-grammar::floatnumber "3.14"))
  (is =
      10.0
      (esrap:parse 'ginjish-grammar::floatnumber "10."))
  (is =
      0.001
      (esrap:parse 'ginjish-grammar::floatnumber ".001"))
  (is =
      1d100
      (esrap:parse 'ginjish-grammar::floatnumber "1e100"))
  (is =
      3.14e-10
      (esrap:parse 'ginjish-grammar::floatnumber "3.14e-10"))
  (is =
      0.0
      (esrap:parse 'ginjish-grammar::floatnumber "0e0"))
  (is =
      3.141593
      (esrap:parse 'ginjish-grammar::floatnumber "3.14_15_93")))
      
(define-test identifier
  :parent parser
  (is eq
      t
      (esrap:parse 'ginjish-grammar::identifier "True"))
  (is eq
      t
      (esrap:parse 'ginjish-grammar::identifier "true"))
  (is eq
      nil
      (esrap:parse 'ginjish-grammar::identifier "False"))
  (is eq
      nil
      (esrap:parse 'ginjish-grammar::identifier "false"))
  (is equal
      '(:identifier "foo")
      (esrap:parse 'ginjish-grammar::identifier "foo"))
  (is equal
      '(:identifier "__foo47__")
      (esrap:parse 'ginjish-grammar::identifier "__foo47__")))

(define-test literal
  :parent parser
  (is =
      3
      (esrap:parse 'ginjish-grammar::literal "3"))
  (is equal
      '(:string "foo")
      (esrap:parse 'ginjish-grammar::literal "'foo'")))

(define-test expression
  :parent parser
  (is equal
      '(:minus (:plus 3 4) 7)
      (esrap:parse 'ginjish-grammar::expression "3 + 4 - 7"))
  (is equal
      '(:plus 3 (:mul 4 7))
      (esrap:parse 'ginjish-grammar::expression "3 + 4 * 7"))
  (is equal
      '(:mul 3 (:minus 4 7))
      (esrap:parse 'ginjish-grammar::expression "3 * (4 - 7)"))
  (is equal
      '(:if-expr (:lt (:identifier "a") (:identifier "b"))
        t
        (:get-item
         (:get-attr (:identifier "default") (:identifier "x"))
         (3 4)))
      (esrap:parse 'ginjish-grammar::expression "true if a < b else default.x[3,4]"))
  (is equal
      '(:if-expr (:lt (:identifier "a") (:identifier "b"))
        t
        (:slicing
         (:get-attr (:identifier "default") (:identifier "x"))
         (:proper-slice 3 4 nil)))
      (esrap:parse 'ginjish-grammar::expression "true if a < b else default.x[3:4]")))

(define-test enclosure
  :parent parser
  (is equal
      1
      (esrap:parse 'ginjish-grammar::enclosure "(1)"))
  (is equal
      '(:tuple 1)
      (esrap:parse 'ginjish-grammar::enclosure "(1,)"))
  (is equal
      '(:list 1)
      (esrap:parse 'ginjish-grammar::enclosure "[1]"))
  (is equal
      '(:set 1)
      (esrap:parse 'ginjish-grammar::enclosure "{1}"))
  (is equal
      '(:dict (1 2))
      (esrap:parse 'ginjish-grammar::enclosure "{1:2}"))
  (is equal
      '(:list 1 2 3)
      (esrap:parse 'ginjish-grammar::enclosure "[1, 2 , 3 ]")))

(define-test call
  :parent parser
  (is equal
      '(:invoke (:identifier "foo") (1 2))
      (esrap:parse 'ginjish-grammar::call "foo(1,2)"))
  (is equal
      '(:invoke (:identifier "foo") ((:identifier "a") :a 1))
      (esrap:parse 'ginjish-grammar::call "foo(a,a=1)")))

(define-test filter
  :parent parser
  (is equal
      '(:filter (:identifier "foo") ("upper" ()) ("truncate" (12)))
      (esrap:parse 'ginjish-grammar::expression "foo | upper|truncate(12)")))

(define-test test
  :parent parser
  (is equal
      '(:test (:identifier "foo") ("zero" ()))
      (esrap:parse 'ginjish-grammar::expression "foo is zero"))
  (is equal
      '(:test-not (:identifier "foo") ("zero" ()))
      (esrap:parse 'ginjish-grammar::expression "foo is not zero")))

;;; compiler

(define-test compiler)

(defclass load-value-example ()
  ((a :initarg :a :accessor lve-a)
   (b :initarg :b :accessor lve-b)))

(defclass load-value-example2 (load-value-example)
  ((c :initarg :c :accessor lve-c)))

(define-test load-value
  :parent compiler
  (is equal
      1
      (ginjish-compiler::load-value '(1 3 3) 0))
  (is equal
      #\f
      (ginjish-compiler::load-value "foo" 0))
  (is equal
      1
      (ginjish-compiler::load-value (list "a" 1 'b 2) 'a))
  (is equal
      1
      (ginjish-compiler::load-value '((a . 1) (b . 2)) 'a))
  (is equal
      1
      (ginjish-compiler::load-value (serapeum:dict "a" 1 "b" 2) "a"))
  (is equal
      1
      (ginjish-compiler::load-value (make-instance 'load-value-example :a 1 :b 2) 'a))
  (is equal
      1
      (ginjish-compiler::load-value (make-instance 'load-value-example2 :a 1 :b 2 :c 3) 'a))
  (is equal
      1
      (ginjish-compiler::load-value (make-instance 'load-value-example :a 1 :b 2) 'lve-a))
  (is equal
      1
      (ginjish-compiler::load-value (make-instance 'load-value-example2 :a 1 :b 2 :c 3) 'lve-a)))

(define-test setf-load-value
  :parent compiler
  (is equal
      '(1 3 3)
      (let ((*context* (list 2 3 3))) (setf (ginjish-compiler::load-value *context* 0) 1) *context*))
  (is equal
      '(a 1)
      (let ((*context* nil)) (setf (ginjish-compiler::load-value *context* 'a) 1) *context*))
  (is equal
      '(a 1 b 2)
      (let ((*context* (list 'b 2))) (setf (ginjish-compiler::load-value *context* 'a) 1) *context*))
  (is equal
      '(a 1 b nil)
      (let ((*context* (list 'a nil 'b nil)))
        (setf (ginjish-compiler::load-value *context* 'a) 1)
        *context*))
  (is equal
      '(a nil)
      (let ((*context* nil))
        (setf (ginjish-compiler::load-value *context* 'a) nil)
        *context*))
  (is equal
      "foo"
      (let ((*context* "woo")) (setf (ginjish-compiler::load-value *context* 0) #\f) *context*))
  (is equal
      '((a . 2) (b . 2))
      (let ((*context* (list (cons 'a  1) (cons 'b  2))))
        (setf (ginjish-compiler::load-value *context* 'a) 2)
        *context*))
  (is equal
      '(("a" . 2) ("b" . 2))
      (let ((*context* (serapeum:dict "a" 1 "b" 2)))
        (setf (ginjish-compiler::load-value *context* "a") 2)
        (sort (alexandria:hash-table-alist *context*) #'string< :key #'car)))
  (is equal
      2
      (let ((*context* (make-instance 'load-value-example :a 1 :b 2)))
        (setf (ginjish-compiler::load-value *context* 'a) 2)
        (slot-value *context* 'a)))
  (is equal
      2
      (let ((*context* (make-instance 'load-value-example :a 1 :b 2)))
        (setf (ginjish-compiler::load-value *context* 'lve-a) 2)
        (lve-a *context*)))
  (is equal
      2
      (let ((*context* (make-instance 'load-value-example2 :a 1 :b 2 :c 3)))
        (setf (ginjish-compiler::load-value *context* 'lve-a) 2)
        (lve-a *context*))))

(define-test truthy
  :parent compiler
  (false (ginjish-compiler::truthy nil))
  (true (ginjish-compiler::truthy t))
  (false (ginjish-compiler::truthy 0))
  (true (ginjish-compiler::truthy 1))
  (false (ginjish-compiler::truthy 0.0))
  (true (ginjish-compiler::truthy 1.0))
  (false (ginjish-compiler::truthy ""))
  (true (ginjish-compiler::truthy "a"))
  (false (ginjish-compiler::truthy #()))
  (true (ginjish-compiler::truthy #(1)))
  (false (ginjish-compiler::truthy (make-hash-table)))
  (true (ginjish-compiler::truthy (serapeum:dictq a 1))))
      
(define-test gte
  :parent compiler
  (true (ginjish-compiler::gte 1 0))
  (false (ginjish-compiler::gte 0 1))
  (true (ginjish-compiler::gte 1 1))
  (true (ginjish-compiler::gte "foo" "bar"))
  (false (ginjish-compiler::gte "bar" "foo"))
  (true (ginjish-compiler::gte "bar" "bar"))
  (true (ginjish-compiler::gte '(1 2) '(1)))
  (false (ginjish-compiler::gte '(1) '(1 2)))
  (true (ginjish-compiler::gte '(2) '(1)))
  (true (ginjish-compiler::gte '(1 2 3) '(1 2 3)))
  (true (ginjish-compiler::gte '(2) '(1 2 3)))
  (true (ginjish-compiler::gte '(1 (2) 3) '(1 (1) 3))))

(defun if-test-helper (string &optional env)
  (let ((ginjish-compiler::*context* env))
    (with-output-to-string (s)
      (funcall (ginjish-compiler::compile-element
                (esrap:parse 'ginjish-grammar::template
                             string))
               s))))

(define-test if*
  :parent compiler)

(define-test if-simple
  :parent if*
  (is string=
      "..."
      (if-test-helper "{% if true %}...{% endif %}")))

(define-test if-elif
  :parent if*
  (is string=
      "..."
      (if-test-helper "{% if false %}XXX{% elif true %}...{% else %}XXX{% endif %}")))

(define-test if-elif-deep
  :parent if*
  (is string=
      "0"
      (if-test-helper (format nil "{% if a == 0 %}0屐殒浇邯宁屐箦孱溟ㄡ戾犷潋獒洪雉构后翎螋暴Ж⑨癌┅ㄩ篝蜷铉⒈阿ㄩ姝翦篝桢祓弪ㄦ矧磲铋Ⅺ殒浇剥屐殒浇邯宁屐箦孱溟ㄡ戾犷潋獒洪雉构后翎螋暴Ж⑨卑┅ㄩ篝蜷铉⒐构ㄩ姝翦篝桢祓弪ㄦ矧磲铋Ⅺ殒浇剥屐殒浇邯宁屐箦孱溟ㄡ戾犷潋獒洪雉构后翎螋暴Ж⑨构供┅ㄩ篝蜷铉Ⅷㄩ姝翦篝桢祓弪ㄦ矧磲铋Ⅺ殒浇剥屐殒浇邯宁屐箦孱溟ㄡ戾犷潋獒洪雉构后翎螋暴Ж⑨卑鞍┅┅ㄤ彐轭瀛翦篝殒屐箦吼狎孱殒ㄩ篝蜷铉ㄩ姝翦篝桢祓弪Ⅺ殒驷祗刎佧屐箦孱溟┅ㄤ彐轭瀛翦篝殒屙痿吼狎孱殒ㄩ篝蜷铉③茛ㄩ姝翦篝桢祓弪③殒趄蹂屐箦孱溟茛┅ㄤ彐轭瀛翦篝殒泔眇戾翦吼狎孱殒ㄩ篝蜷铉⒚ㄩ姝翦篝桢祓弪Ⅺ殒聋屐殒蔓屐殒浇名屐箦柠孱溟Ж⑨⑩铋恽床洧床癌┅ㄤ彐轭瀛翦篝殒铒筱镳吼狎孱殒ㄩ篝蜷铉⒈ㄩ姝翦篝桢祓弪Ⅺ殒箦骘孱溟骘Ж⑨舂┅ㄩ篝蜷铉⒈ㄩ姝翦篝桢祓弪Ⅺ殒趄蹂箦骘孱溟骘┅ㄤ彐轭瀛翦篝鏖翳吼狎孱泔眇殪弪ㄩ篝蜷铉⒋渤并ㄩ姝翦篝桢祓弪Ⅺ鏖翳峤床饨渤孱澉轸Ж博┅ㄩ篝蜷铉⒈颤滁袋耽ㄩ姝翦篝桢祓弪Ⅺ鏖翳峤爆饨铂憬猬浣瀣褰孱澉轸Ж穿┅ㄤ彐轭瀛翦篝骘吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝骘颦箝眇戾吼狎孱骘ㄩ篝蜷铉氨渤吹斗腹ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦轸屙孱滏矧啜箦ㄡ戾犷潋獒洪雉卑┅┅ㄤ彐轭瀛翦篝骘颦屐箦吼狎孱骘ㄩ篝蜷铉ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦刎佧屐箦孱滏矧┅ㄤ彐轭瀛翦篝骘颦屐箦筱镳轭吼狎孱骘ㄩ篝蜷铉⒋并ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭圯屐箦轸屙孱滏矧Ж轸屙床┅┅ㄤ彐轭瀛翦篝骘颦屙痿忪镢塍吼狎孱骘ㄩ篝蜷铉⒓劲ㄩ姝翦篝桢祓弪⒓骘轸屙轭箦屐箦孱滏矧劲┅ㄤ彐轭瀛翦篝骘颦泔铘屮舡鲠蝮吼狎孱骘ㄩ篝蜷铉⒈包滁颤则蹂漆祗妩常＃颤秉颤秉漆祗妩漆祗妩常＃滁颤秉包漆祗妩则蹂常＃ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦祜镳轭溴祜镳轭溴祜镳蝈鲩钿屮祜镳蝈鲩钿屮祜镳骈蝮祜镳灬篝祜镳戾铉翳＃｛孱滏矧扉篝箦Ж畅┅┅ㄤ彐轭瀛翦篝骘颦泫沆轭吼狎孱骘箅轲⒃夏滹灬翦颌ㄩ篝蜷铉⒓本疾炯本疾炯本疾炯本疾劲ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦祜镳泫沆濞Ъ本КЪ簿З孱滏矧骘轸屙轭箦祜镳泫沆濞翳蝻蹒枸陛翳蝻蹒枸草孱滏矧扉篝箦ㄡ戾犷潋獒洪雉穿翳蝻蹒扉篝⒓本⒓簿┅┅┅ㄤ彐轭瀛翦篝骘颦祜镫狎秕钿吼狎孱骘ㄩ篝蜷铉Ⅷ碍秉碍杯颤杯箔滁箔抄ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦祜镳痱弼轸屙溴驷蹯舁З轸屙祜镳铄糸翦睃溴驷蹯舁З孱滏矧扉篝Ⅲ羼ㄡ戾犷潋獒洪雉穿┅┅ㄤ彐轭瀛翦篝骘颦汨犷珏粕赝祜镳汨犷珏洙铋飑轶犰麽漆祗吼狎孱骘ㄩ篝蜷铉⒃蝓瀣漆祗瀣则蹂则蹂漆祗瀣则蹂则蹂漆祗瀣漆祗瀣ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦祜镳汨犷珏洙轸屙孱滏矧扉篝箦Ж穿┅┅ㄤ彐轭瀛翦篝骘颦筱镳吼狎孱骘ㄩ篝蜷铉ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦孱滏矧轸屙扉篝箦ㄡ戾犷潋獒洪雉卑┅┅ㄤ彐轭瀛翦篝骘颦鲠蜢孱吼狎孱骘ㄩ篝蜷铉氨渤储ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭轸弪轸屙孱滏矧扉篝ч翦ㄡ戾犷潋獒洪雉旦┅┅ㄤ彐轭瀛翦篝骘颦铒铋翦粕赝轶铋忮趑弪蝈箴镱箦吼狎孱骘ㄦ衢ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭铒铄孱滏矧┅ㄤ彐轭瀛翦篝骘颦蝈沲蝮轹粕赝轭篝犰骢钽糸镱滔闲轭珈镡犰钺礤箴徙麸汜祆翳轶怙澌吼狎孱骘箅轲⒃夏滹灬翦颌ㄩ篝蜷铉③奔郾蒇草据鄄价陛鄄菥蒇臣坩菥茛ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦蝈沲蝮轹埯祜镳痱弼轸屙殒祜镳痱弼轸屙轶溴骈铄屐箦轸屙祜镳铄糸翦懋殒祜镳铄糸翦轶溴骈铄屐箦殒轸屙见祜镳ㄩ翦懋猢钧孱溟蓰孱滏矧扉篝箦扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п⑨┅┅┅┅ㄤ彐轭瀛翦篝骘颦蝈沲蝮轹瀛祜镫狎秕钿吼狎孱骘箅轲⒃夏滹灬翦颌ㄩ篝蜷铉③碑布埒碑草郾伯菥蒇碑伯臣埒碑草郾伯菥蒇伯钞价岙菥茛ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦蝈沲蝮轹埯祜镳痱弼轸屙殒祜镳痱弼轸屙轶溴骈铄屐箦轸屙祜镳铄糸翦懋殒祜镳铄糸翦轶溴骈铄屐箦殒轸屙见祜镳ㄩ翦懋猢钧孱溟蓰孱滏矧扉篝箦扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п⑨┅┅┅┅ㄤ彐轭瀛翦篝骘颦蝈沲蝮轹瀛溴痿璋吼狎孱骘箅轲⒃夏滹灬翦颌ㄩ篝蜷铉③昂奔郾罕蒇焙草据郯翰价焙陛郾翰菥蒇昂臣郾横菥茛ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦蝈沲蝮轹埯祜镳溴痿璋蝴轸屙殒轸屙见祜镳ㄩ翦懋猢钧孱溟蓰孱滏矧扉篝箦扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п⑨┅┅┅┅ㄤ彐轭瀛翦篝骘颦蝈沲蝮轹瀛溴痿吼狎孱骘箅轲⒃夏滹灬翦颌ㄩ篝蜷铉③焙奔鄄罕蒇埠草据郾翰价埠陛鄄翰菥蒇焙臣鄄横菥茛ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦蝈沲蝮轹埯祜镳溴痿蝴轸屙殒轸屙见祜镳ㄩ翦懋猢钧孱溟蓰孱滏矧扉篝箦扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п暴扉篝п博┅扉篝пр扉篝扉篝п⑨┅┅┅┅ㄤ彐轭瀛翦篝骘颦祜镳祜镳吼狎孱骘ㄩ篝蜷铉③秉陛郾草鄄陛鄄草ㄩ姝翦篝桢祓弪Ⅺ骘蝻轭翎忪箦蝻黛镲祜镳骘沐祆轭蝻埯蝻黛镲甬轭溴祜镳轭溴蓰孱滏矧孱滏矧扉篝翎忪扉篝⑨猗沅┅┅ㄤ彐轭瀛翦篝骘颦蝈鲥蝮邃怩吼狎孱骘ㄩ篝蜷铉⒈铂尝ㄩ姝翦篝桢祓弪Ⅺ骘轭轸屙殒铒祜镳灬篝孱溟孱滏矧扉篝ч翦眢蝈鲥蝮扉篝暴┅┅ㄤ彐轭瀛翦篝骘颦祜镳弪蝻蝮粕赝鏖祆减钿彐轭邃桢祓吼狎孱骘ㄦ衢ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭郾殒祜镳轭溴浇孱滏矧┅ㄤ彐轭瀛翦篝骘颦祜镳骈祠弪翦篝蜥翳弪吼狎孱骘ㄩ篝蜷铉③拜鄄蒇摧鄱蒇篙ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭蜥铉濞卑殒轸屙轶弼孱埯轸屙蓰孱滏矧扉篝ㄣ镱Ⅱ犷珏х轭觊箬怩殪糸铙汉蜥铉濠┅┅ㄤ彐轭瀛翦篝骘颦祜镳躅狍箝珙徕戾粕赝轶铋忮趑弪犷篦弪吼狎孱骘ㄦ衢ㄩ姝翦篝桢祓弪Ⅺ骘祜镳轭箦孱滏矧┅ㄤ彐轭瀛翦篝骘颦筱镳邃箴邈獒飙鲠吼狎孱骘ㄩ篝蜷铉③则蹂则蹂漆祗遢燮犰箦则蹂漆祗遢ㄩ姝翦篝桢祓弪Ⅺ骘轭箦埯祜镳骈蝮骘轭祜镳骈蝮孱滏矧蓰孱滏矧扉篝箦扉篝⑨猗沅┅┅ㄤ彐轭瀛翦篝骘颦筱镳邃祜镳鲠吼狎孱骘ㄩ篝蜷铉⒃蝓迤犰箦ㄩ姝翦篝桢祓弪Ⅺ骘轭箦祜镳骈蝮骘轭箦孱滏矧孱滏矧扉篝箦⑨猗┅ㄩ篝蜷铉⒃蝓迤犰箦则蹂漆祗澧ㄩ姝翦篝桢祓弪Ⅺ骘轭箦骘轭箦祜镳骈蝮孱滏矧孱滏矧扉篝箦⑨猗┅┅ㄤ彐轭瀛翦篝骘颦蝈沲蝮轹瀛屙痿祜镳轸弪吼狎孱骘ㄩ篝蜷铉ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭骘蝈沲蝮轹孱滏矧扉篝ф镲铋飑┅换韵南汜祆唛钸祜镳筱镳轭邕怩ㄤ彐轭瀛翦篝骘颦躅疳汶轭吼狎孱骘ㄩ篝蜷铉⒈颤尝ㄩ姝翦篝桢祓弪Ⅺ骘岈猬轭圹爆铂齿孱滏矧┅ㄤ彐轭瀛翦篝骘颦轭翦钿邃筱镳轭绛鏖翳箦吼狎孱骘ㄩ篝蜷铉氨安俺ㄩ姝翦篝桢祓弪Ⅺ骘轸屙轭箦箦轸屙孱滏矧扉篝箦扉篝畅┅ㄩ篝蜷铉⒐惫补尝ㄩ姝翦篝桢祓弪Ⅺ箦骘轸屙轭箦箦轸屙孱滏矧扉篝箦扉篝畅┅┅换磲泸ㄤ彐轭瀛翦篝磲泸吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝磲泸锃箝眇戾吼狎孱磲泸ㄩ篝蜷铉⒈箴犴彗珞铂箴犴伍超陪腴彗珞ㄩ姝翦篝桢祓弪Ⅺ磲泸骘铷忉颥忉涧箴犴К聃貘涧彗珞З忉螨忉聃貘孱漤徙蝻骘铷暴骘铷铂聃貘涧伍З骘铷超忉涧陪腴З┅ㄩ篝蜷铉⒉番船并ㄩ姝翦篝桢祓弪Ⅺ磲泸骘铷忉颥忉聃貘聃貘忉忉螨孱漤徙蝻骘铷鲍爆勃铂唱畅┅换轭沆蹁ㄤ彐轭瀛翦篝轭沆蹁吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝轭沆蹁瀛箝眇戾吼狎孱轭沆蹁ㄩ篝蜷铉㈣屐祜黠蜢洧戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁р狎铋铋飑┅ㄤ彐轭瀛翦篝轭沆蹁瀛扉篝吼狎孱轭沆蹁ㄩ篝蜷铉㈣屐祜黠蜢洧戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁郄忉颛я貂л铋铋飑┅ㄤ彐轭瀛翦篝轭沆蹁瀛黹篌轭吼狎孱轭沆蹁ㄩ篝蜷铉㈣屐祜黠蜢洧戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁郄聃貘Кр狎л铋铋飑┅ㄦ衢戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁я貂铋铋飑┅ㄩ篝蜷铉㈣屐祜戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁я貂殓铒蝈黹篌轭铋铋飑┅ㄩ篝蜷铉㈣屐祜戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲㈣屐祜⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁郄聃貘л殓铒蝈黹篌轭铋铋飑┅ㄤ彐轭瀛翦篝轭沆蹁瀛泔铘屮吼狎孱轭沆蹁ㄩ篝蜷铉㈣屐祜黠蜢洧戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲Ⅺ聃貘⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲轭沆蹁р狎铋扉篝Ⅰ貂㈣屐祜┅┅ㄩ篝蜷铉黠蜢洧戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄧ轭觊箬泔眇殪弪汉磲脲溟泗祜徜弪㈡镲Ⅺ聃貘⑩狎Ⅶ矧熹┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉Ⅺ轭沆蹁ф镲鏖翳秕泔铘屮轭沆蹁р狎铋扉篝Ⅰ貂㈣屐祜┅┅换忪镢屮翦钿ㄤ彐躅忪镢氕翦篝桢祓弪翦眇灬翦篝蜷铉泔铘屮蝈篝翦眇灬翦螬戾è玳铌轶璀泔眇殪弪汉祜徜弪ㄡ痧禊＇玳铌轶璀泔眇殪弪汉磲脲溟泗祜徜弪翦眇灬翦螬┅ㄧ轭觊箬泔眇殪弪汉蝈钿弪翦眇灬翦ㄧ轭觊箬泔眇殪弪汉泔眇殪瀛翦眇灬翦篝蜷铉翦眇灬翦篝蜷铉铋泔铘屮舂┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝忪镢氕箝眇戾吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ忪镢骘桢祆稃孱溻祜汶忪镢忉黠蜢潲孱溻祜汶铋飑┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓箝眇戾吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ屮翦钿ф镲忪镢桢祆稃孱溻祜汶忪镢黠蜢潲孱溻祜汶铋㈡镲Ⅺ忪镢孱溻祜汶忪镢孱溻祜汶┅ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ屮翦钿ф镲忪镢桢祆稃孱溻祜汶忪镢黠蜢潲孱溻祜汶铋㈡镲Ⅺ忪镢顼镤怡妍孱溻祜汶忪镢痫翎麸孱溻祜汶┅ㄩ篝蜷铉㈣屐祜痫翎麸ㄢ祜汶翦篝桢祓弪Ⅺ屮翦钿ф镲忪镢桢祆稃孱溻祜汶铋㈡镲Ⅺ忪镢顼镤怡妍孱溻祜汶忪镢痫翎麸孱溻祜汶┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓铄篝邃吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉ㄨ屐祜黠蜢洎ㄢ祜汶翦篝桢祓弪Ⅺ屮翦钿ф镲忪镢桢祆稃孱溻祜汶铋㈡镲Ⅺ屮翦钿р狎忪镢黠蜢潲孱溻祜汶⑩狎忪镢孱溻祜汶忪镢孱溻祜汶┅ㄤ彐疳蜥礤翦塘傧赵耘托塘耘Ⅻ忪镢忪镢氡忪镢骝镯灬秕酐孱溻祜汶忪镢忪镢氩忪镢骝镯灬秕酐孱溻祜汶忪镢忪镢氤忪镢忪镢氪铄篝邃忪镢骝镯灬秕酐孱溻祜汶孱溻祜汶ㄤ彐疳蜥礤翦膛峙瘫耘托塘耘Ⅺ屮翦钿ъ狴秕臾忪镢忪镢氡忪镢骝镯戾鲥毂孱溻祜汶ㄤ彐疳蜥礤翦膛峙滩耘托塘耘Ⅺ屮翦钿ъ弼屐抱忪镢忪镢氩忪镢忪镢氲铄篝邃忪镢骝镯戾鲥觳孱溻祜汶孱溻祜汶ㄤ彐疳蜥礤翦膛峙坛耘托塘耘Ⅺ屮翦钿ъ弼屐钵忪镢忪镢氲忪镢骝镯戾鲥斐孱溻祜汶忪镢忪镢氪忪镢骝镯戾鲥斐孱溻祜汶ㄤ彐疳蜥礤翦膛峙檀耘托塘耘Ⅺ屮翦钿ъ弼屐厂忪镢忪镢氤忪镢骝镯戾鲥齑孱溻祜汶ㄤ彐疳蜥礤翦紫宜晌窃磐刑猎Ⅺ屮翦钿ъ狴秕臾忪镢忪镢氡殒驷祗忪镢忪镢氩翳轶箬秕熹黠螂孱溻祜汶孱溟孱溻祜汶ㄤ彐疳蜥礤翦南章膛咆耘文Ⅺ屮翦钿ъ狴秕臾屮翦钿ъ狴秕臾忪镢忪镢氡殒驷祗忪镢忪镢氩翳轶箬秕熹黠螂孱溻祜汶孱溟孱溻祜汶ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓灬秕吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉Ⅻ忪镢骝镯灬秕酎忪镢骝镯灬秕酎铄篝邃忪镢骝镯灬秕酎戾è玳铌轶璀泔眇殪弪汉趄轫忪镢塍舂ㄢ祜汶翦篝桢祓弪塘傧赵耘托塘耘铋㈧狴秕簪塘傧赵耘托塘耘㈧弼屐雹膛峙瘫耘托塘耘㈧弼屐并膛峙滩耘托塘耘㈧弼屐尝膛峙坛耘托塘耘㈧弼屐储膛峙檀耘托塘耘Ⅶ矧腴铉紫宜晌窃磐刑猎滹踱戾澧南章膛咆耘文萤┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓戾鲥毂吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉Ⅻ忪镢骝镯戾鲥毂忪镢骝镯灬秕酎铄篝邃忪镢骝镯灬秕酎戾è玳铌轶璀泔眇殪弪汉趄轫忪镢塍舂ㄢ祜汶翦篝桢祓弪膛峙瘫耘托塘耘铋㈧狴秕簪塘傧赵耘托塘耘㈧弼屐雹膛峙瘫耘托塘耘㈧弼屐并膛峙滩耘托塘耘㈧弼屐尝膛峙坛耘托塘耘㈧弼屐储膛峙檀耘托塘耘Ⅶ矧腴铉紫宜晌窃磐刑猎滹踱戾澧南章膛咆耘文萤┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓戾鲥觳吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉Ⅻ忪镢骝镯戾鲥毂铄篝邃忪镢骝镯戾鲥觳铄篝邃忪镢骝镯灬秕酎戾è玳铌轶璀泔眇殪弪汉趄轫忪镢塍舂ㄢ祜汶翦篝桢祓弪膛峙滩耘托塘耘铋㈧狴秕簪塘傧赵耘托塘耘㈧弼屐雹膛峙瘫耘托塘耘㈧弼屐并膛峙滩耘托塘耘㈧弼屐尝膛峙坛耘托塘耘㈧弼屐储膛峙檀耘托塘耘Ⅶ矧腴铉紫宜晌窃磐刑猎滹踱戾澧南章膛咆耘文萤┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓戾鲥斐吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉Ⅻ忪镢骝镯戾鲥毂忪镢骝镯戾鲥斐忪镢骝镯戾鲥斐戾è玳铌轶璀泔眇殪弪汉趄轫忪镢塍舂ㄢ祜汶翦篝桢祓弪膛峙坛耘托塘耘铋㈧狴秕簪塘傧赵耘托塘耘㈧弼屐雹膛峙瘫耘托塘耘㈧弼屐并膛峙滩耘托塘耘㈧弼屐尝膛峙坛耘托塘耘㈧弼屐储膛峙檀耘托塘耘Ⅶ矧腴铉紫宜晌窃磐刑猎滹踱戾澧南章膛咆耘文萤┅ㄤ彐轭瀛翦篝忪镢氕屮翦钿蟓戾鲥齑吼狎孱忪镢氕屮翦钿ㄩ篝蜷铉Ⅻ忪镢骝镯戾鲥毂忪镢骝镯戾鲥斐忪镢骝镯戾鲥齑戾è玳铌轶璀泔眇殪弪汉趄轫忪镢塍舂ㄢ祜汶翦篝桢祓弪膛峙檀耘托塘耘铋㈧狴秕簪塘傧赵耘托塘耘㈧弼屐雹膛峙瘫耘托塘耘㈧弼屐并膛峙滩耘托塘耘㈧弼屐尝膛峙坛耘托塘耘㈧弼屐储膛峙檀耘托塘耘Ⅶ矧腴铉紫宜晌窃磐刑猎滹踱戾澧南章膛咆耘文萤┅换轫痫螋ㄤ彐轭瀛翦篝轫痫螋吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝轫痫螋箝眇戾吼狎孱轫痫螋ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ轫痫螋ф镲狍骘骘锂忉颞铋㈡镲Ⅺ磲泸忉颞桢祆黠蜢潲孱漤徙蝻┅ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ轫痫螋ф镲狍骘骘锂忉颞骘锂聃貘铋㈡镲Ⅺ磲泸忉颞桢祆孱漤徙蝻箦聃貘黠蜢洄┅ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ轫痫螋ф镲狍骘骘锂忉颞骘锂忉ī铋㈡镲Ⅺ磲泸忉ī黠蜢潲孱漤徙蝻磲泸忉颞桢祆稃孱漤徙蝻┅ㄤ彐轭瀛翦篝骝镯箝眇戾吼狎孱轫痫螋ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ骝镯ф镲轫痫螋忉狍猕铋㈡镲Ⅺ磲泸忉颞桢祆黠蜢潲孱漤徙蝻┅ㄩ篝蜷铉㈣屐祜黠蜢洧ㄢ祜汶翦篝桢祓弪Ⅺ骝镯ф镲轫痫螋忉颥忉狍聃貘忉颞聃貘ī铋㈡镲Ⅺ磲泸忉ī黠蜢潲孱漤徙蝻磲泸忉颞桢祆稃孱漤徙蝻┅换簌铘狲ㄤ彐轭瀛翦篝簌铘狲吼狎孱泔眇殪弪ㄤ彐轭瀛翦篝簌铘狲箪殂轭吼狎孱簌铘狲ㄩ篝蜷铉③爆铂齿鄢铂陛ㄩ姝翦篝桢祓弪Ⅺ郾铂齿酆郾铂齿酆涵陛┅ㄤ彐轭瀛翦篝簌铘狲狒趄吼狎孱簌铘狲ㄩ篝蜷铉⒋颤床ㄩ姝翦篝桢祓弪Ⅺ骘锂忉骘镗р狎л扉篝ф镲箦蜥疱蹴轰殂⑩狎床┅┅ㄤ彐轭瀛翦篝簌铘狲篚怏泸轲吼狎孱簌铘狲ㄩ篝蜷铉包并ㄩ姝翦篝桢祓弪Ⅺ骘镗拜骘镗陛扉篝ф镲扉篝博┅┅换趱痨ㄤ彐轭瀛翦篝簌铘狲磲翳吼狎孱簌铘狲ㄩ篝蜷铉⒈迭涪ㄩ姝翦篝桢祓弪Ⅺū博勃┅ㄤ彐轭瀛翦篝簌铘狲溟ㄩ篝蜷铉⒈碑迭雹ㄩ姝翦篝桢祓弪Ⅺ┅ㄤ彐轭瀛翦篝簌铘狲躅狎吼狎孱簌铘狲ㄩ篝蜷铉⒊尝ㄩ姝翦篝桢祓弪Ⅺ┅ㄤ彐轭瀛翦篝簌铘狲泔钽狒吼狎孱簌铘狲ㄩ篝蜷铉③爆草骘铫ㄩ姝翦篝桢祓弪Ⅺ郾草ф镲┅ㄤ彐轭瀛翦篝簌铘狲轭镳吼狎孱簌铘狲ㄩ篝蜷铉⒃蝓妩漆祗澧ㄩ姝翦篝桢祓弪Ⅺ轭郾铂齿铒轭郾铂齿┅ㄤ彐轭瀛翦篝簌铘狲怙镬吼狎孱簌铘狲ㄩ篝蜷铉⑵犰箦则蹂则蹂ㄩ姝翦篝桢祓弪Ⅺ趄蹂犷驷祗驷祗矧趄蹂铒驷祗┅ㄤ彐轭瀛翦篝簌铘狲珧秕痖铉吼狎孱簌铘狲ㄩ篝蜷铉⑵犰箦ㄩ姝翦篝桢祓弪Ⅺ趄蹂犷驷祗濠矧ㄦ犰箦犷趄蹂犷铒驷祗┅ㄤ彐轭瀛翦篝簌铘狲潢犷顼狒趄吼狎孱簌铘狲箅轲⒃夏滹麇蝈犰禊麽铘麸篚痧矧翳轶竣ㄩ篝蜷铉⒈雹ㄩ姝翦篝桢祓弪Ⅺ郾铂齿圹陛莓爱┅┅ㄤ彐轭瀛翦篝簌铘狲泔钿轸轱钺飙屮痱弩箝镱吼狎孱簌铘狲ㄩ篝蜷铉阿ㄩ姝翦篝桢祓弪Ⅺ殒趄蹂屐箦┅ㄩ篝蜷铉⒓劲ㄩ姝翦篝桢祓弪⒓殒驷祗劲┅ㄤ彐轭瀛翦篝簌铘狲骈祠弪痱轱蜷豉吼狎孱簌铘狲ㄩ篝蜷铉⑵舷铝尧ㄩ姝翦篝桢祓弪Ⅺф镲躔疱р狎躔疱┅ㄤ彐轭瀛翦篝簌铘狲翦篝汨衢铋铉吼狎孱簌铘狲箅轲⒃夏蝈灬翦麸忾钿轭镦骈祠弪翦篝竣ㄩ篝蜷铉⒃蝓澧ㄩ姝翦篝桢祓弪Ⅺ床轶篝蜷铉矧床轶铛礅弪┅ㄦ衢ㄩ姝翦篝桢祓弪Ⅺ骘轶篝蜷铉轶箦聃孱沐┅ㄤ彐轭瀛翦篝簌铘狲篝蜷铉泔钽狒孱狒轱吼狎孱簌铘狲ㄩ篝蜷铉㈡镲忉蜮狷ㄩ姝翦篝桢祓弪Ⅺф镲堍忉蜍р狷┅ㄤ彐轭瀛翦篝簌铘狲铒糸吼狎孱簌铘狲ㄩ篝蜷铉⑵犰箦ㄩ姝翦篝桢祓弪Ⅺ铒床轭忉扉篝р狎ㄡ戾犷潋獒洪雉卑癌┅┅ㄤ彐轭瀛翦篝簌铘狲镳弪狒矧痱邈邃孱沐吼狎孱簌铘狲ㄩ篝蜷铉⒌ㄩ姝翦篝桢祓弪Ⅺ┅ㄤ彐轭瀛翦篝簌铘狲轫痨殂轸篚怏泸殁邃趱痨粕赝汨犷珏翦篝吼狎孱簌铘狲ㄩ篝蜷铉㈠珑螈ㄩ姝翦篝桢祓弪Ⅺ骘镗爆草扉篝ф镲箦蜥疱蹴轰殂Ж博㈠珑螈┅┅ㄤ彐轭瀛翦篝簌铘狲蜥吼狎孱簌铘狲ㄩ篝蜷铉Ⅺ葡犷铝ㄩ姝翦篝桢祓弪Ⅺ蜥葡犷铝孱潋狩┅ㄤ彐轭瀛翦篝簌铘狲祜汜祗弭吼狎孱簌铘狲ㄩ篝蜷铉阿ㄩ姝翦篝桢祓弪Ⅺ箦骘骘轸屙轭郾草箦骘孱滏矧骘┅ㄤ彐轭瀛翦篝簌铘狲疳蝮瀛躅狎吼狎孱簌铘狲ㄩ篝蜷铉床ㄩ姝翦篝桢祓弪Ⅺ骘镗р狎л扉篝ф镲箦蜥疱蹴轰殂⑩狎床┅┅箅轲⒃夏书铌簌铘狲蝈鲥蝮弩痫麇犷躅狎ㄩ篝蜷铉⒋并ㄩ姝翦篝桢祓弪Ⅺ骘镗堍忉蜍⑤徕扉篝ф镲箦蜥疱蹴轰殂⑩狎床┅┅┅换祗趄轲忪镢塍ㄤ彐轭瀛翦篝祗趄轲忪镢塍ㄣ飙轭翦蝠镬哄钺忪瀛轭翦蝠镬簌铘狲ㄤ彐轭瀛翦篝祗趄轲祗趄轲吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？④睥戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒则蹂茴孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲趄轫吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪？殒则蹂茴孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲铒祗趄轲吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍铋飑ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒则蹂茴孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲忪镢塍驷祗瀛鏖翳铒祗趄轲吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍铋飑ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒则蹂茴孱溟┅ㄩ篝蜷铉？茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍铋飑ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？カ殒则蹂茴孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲孱潇轭吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？桢祆镘顼镤怡澧戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？桢祆稃殒则蹂茴顼镤怡妍孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲轭扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉㈣屐祜戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒则蹂桢祆孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲铄篝邃吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉⑨戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪殒则蹂殒则蹂孱溟孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲戾骠汨狎吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？徕茴桢祆铫戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪徕殒则蹂桢祆稃孱溟┅┅ㄤ彐轭瀛翦篝祗趄轲屙忮滗邃篝蜷铉吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉篝戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪箦堍篝堍┅┅ㄤ彐轭瀛翦篝祗趄轲痱弩弪鲥戾徜轭绛铄黛轭弩吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？④钴钴睥戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？④钴钴铥箦桢祆┅┅ㄤ彐轭瀛翦篝祗趄轲泔眄孱吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？④铊屐祜茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪殒则蹂｝桢祆ｅ钿殒｝┅┅ㄤ彐轭瀛翦篝祗趄轲犷珈瀛怛徙脲舡箝眇戾吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉㈣屐祜ㄧ轭觊箬珧犴磲蚝瑚轸璀溴扉黹翦蝮á讥ゾ⒓ィゾ戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪讥殒则蹂ゾ桢祆讥孱溟ゾ┅┅ㄤ彐轭瀛翦篝祗趄轲犷珈瀛怛徙脲舡泔眄孱吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉㈣屐祜ㄧ轭觊箬珧犴磲蚝瑚轸璀溴扉黹翦蝮á讥ゾ⒓ィゾ戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪讥殒则蹂ゾ桢祆讥孱溟ゾ┅┅ㄤ彐轭瀛翦篝祗趄轲痂瓠簌铘狲鏖翳磲铛犰吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉氨渤储ㄧ轭觊箬珧犴磲蚝瑚轸璀溴扉黹翦蝮á伎⒖劲⒓拷⒖劲⒓…劲戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪⒓…骚泔眄孱衄骚铒轭翦蝈篝轭伎骘轸屙轭箦烤伎轸屙烤伎孱滏矧烤扉篝箦ㄡ戾犷潋獒洪雉旦┅┅┅ㄤ彐轭瀛翦篝祗趄轲弪猸簌铘狲鏖翳磲铛犰吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉氨渤储ㄧ轭觊箬珧犴磲蚝瑚轸璀溴扉黹翦蝮á讥ゾ⒓ソゾ⒓ィゾ戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪⒓ィ骚泔眄孱衄骚铒轭翦蝈篝轭ゾ讥骘轸屙轭箦ゾ讥轸屙ゾ讥孱滏矧ゾ扉篝箦ㄡ戾犷潋獒洪雉旦┅┅┅ㄤ彐轭瀛翦篝祗趄轲忪镢塍秕趔殇瀛鏖翳铄鳝扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？ㄜ钺奖饨茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒膂ㄜ骘氍轭膂禁孱滏矧茴孱溟扉篝щ鲶Жá幄暴á猗博┅┅┅ㄤ彐轭瀛翦篝祗趄轲趄轫忪镢塍秕趔殇瀛鏖翳铄鳝扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？ㄜ钺奖饨戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪？殒膂ㄜ骘氍轭膂禁孱滏矧茴孱溟扉篝щ鲶Жá幄暴á猗博┅┅┅ㄤ彐轭瀛翦篝祗趄轲忪镢塍轭箝溴鏖翳铄鳝扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？ㄜ钺奖饨茴戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪？殒膂茴骘氍轭膂禁孱滏矧茴孱溟扉篝щ鲶Жá幄暴á猗博┅┅┅ㄤ彐轭瀛翦篝祗趄轲趄轫忪镢塍轭箝溴鏖翳铄鳝扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉？ㄡ奖饨戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍舂ㄩ姝翦篝桢祓弪？殒膂茴骘氍轭膂禁孱滏矧茴孱溟扉篝щ鲶Жá幄暴á猗博┅┅┅ㄤ彐轭瀛翦篝祗趄轲忪镢塍鏖翳秕舡铄鳝扉铄吼狎孱祗趄轲忪镢塍ㄩ篝蜷铉峤饨戾è玳铌轶璀泔眇殪弪汉祗趄轲忪镢塍舂ㄧ轭觊箬泔眇殪弪汉趄轫忪镢塍铋飑ㄩ姝翦篝桢祓弪殒膂骘氍轭膂禁孱滏矧孱溟扉篝щ鲶Жá幄暴á猗博┅┅┅换骈祠弪ㄤ彐轭瀛翦篝骈祠弪螬ㄤ彐轭瀛翦篝骈祠弪蟓汜痖翎扉吼狎孱骈祠弪ㄩ篝蜷铉⑵镲忉颌ㄩ姝翦篝桢祓弪Ⅺф镲忉颛汜痖翎扉┅ㄤ彐轭瀛翦篝骈祠弪蟓沐铘弪吼狎孱骈祠弪ㄩ篝蜷铉骘ㄩ姝翦篝桢祓弪Ⅺф镲沐铘弪ü┅ㄤ彐轭瀛翦篝骈祠弪蟓溴驷蹯吼狎孱骈祠弪ㄩ篝蜷铉㈩稂漆祗妩铒弩ㄩ姝翦篝桢祓弪Ⅺ黹篌轭琰溴驷蹯舁ь铵驷祗妩溴驷蹯舁ь铵驷祗妩溴驷蹯舁ь铵趄蹂玳鲥铧溴驷蹯舁ь铵扉篝х轹孱Ⅸ弩┅┅ㄤ彐轭瀛翦篝骈祠弪蟓溟泗箫螋吼狎孱骈祠弪ㄩ篝蜷铉③ě後К癌ě谅К畅ě猝暴ě悃博茛ㄩ姝翦篝桢祓弪Ⅺ骘稂溟泗箫螋扉篝ф镲箦蜥疱蹴轰殂⑨幄⑩恽⒘垄畅┅ㄩ篝蜷铉③ě谅К畅ě後К癌ě猝暴ě悃博茛ㄩ姝翦篝桢祓弪Ⅺ骘稂溟泗箫螋ㄣ狍暹箦铙轸轹褰则蹂扉篝ф镲箦蜥疱蹴轰殂⑨幄⑩恽⒘垄畅┅ㄩ篝蜷铉③ě後К癌ě猝暴ě悃博ě谅К畅茛ㄩ姝翦篝桢祓弪Ⅺ骘稂溟泗箫螋ㄢ涧鲠祯濮扉篝ф镲箦蜥疱蹴轰殂⑨幄⑩恽⒘垄畅┅ㄩ篝蜷铉③ě悃博ě猝暴ě谅К畅ě後К癌茛ㄩ姝翦篝桢祓弪Ⅺ骘稂溟泗箫螋蝈鲥蝮褰则蹂扉篝ф镲箦蜥疱蹴轰殂⑨幄⑩恽⒘垄畅┅┅ㄤ彐轭瀛翦篝骈祠弪蟓忉翥吼狎孱骈祠弪ㄩ篝蜷铉③郯爆草鄢船递鄱番篙酃葺圹艾爆草鄢船递鄱番篙酃жКжл茛ㄩ姝翦篝桢祓弪Ⅺ骘稂忉翥瑷畅扉篝骘稂忉翥瑷超жЗ扉篝扉篝ф镲ㄡ戾犷潋獒洪雉卑┅┅ㄤ彐轭瀛翦篝骈祠弪蟓箪殂吼狎孱骈祠弪ㄩ篝蜷铉③郯爆铂齿鄞惮遁鄯脯馆蔹圹艾爆铂齿鄞惮冬жл鄯脯宫жл茛ㄩ姝翦篝桢祓弪Ⅺ骘稂箪殂濞畅扉篝骘稂箪殂濞超жЗ扉篝扉篝ф镲ㄡ戾犷潋獒洪雉卑┅┅ㄤ彐轭瀛翦篝骈祠弪蟓弩汜疱吼狎孱骈祠弪ㄩ篝蜷铉祠沪３椿玺沪犴鸹ㄩ姝翦篝桢祓弪ⅪЪ堍睛弩汜疱┅ㄤ彐轭瀛翦篝骈祠弪蟓趄轫吼狎孱骈祠弪ㄩ篝蜷铉篝狴螽ㄩ姝翦篝桢祓弪Ⅺ骘稂趄轫扉篝ф镲篝狴螽┅ㄩ篝蜷铉篝狴螈ㄩ姝翦篝桢祓弪Ⅺ骘稂趄轫ěЗ扉篝ф镲篝狴螽┅ㄩ篝蜷铉Ⅲ翎螈ㄩ姝翦篝桢祓弪Ⅺ骘稂趄轫ěЗ扉篝ф镲篝狴螽┅┅ㄤ彐轭瀛翦篝骈祠弪蟓篝蜷痿徵吼狎孱骈祠弪ㄩ篝蜷铉㈥躞箜犰屮犴痨扉铍麸麇怵徵澧ㄩ姝翦篝桢祓弪Ⅺ骘稂篝蜷痿徵扉篝ф镲拣娟躞箜犰坚栩彐杰＼⒕屮犴痨寮峋扉铍集鹁拣爵麇怵徵寮鹁肌拣踞钿箫礤泔眄孱翦篝蹑婕鹁劲┅┅ㄤ彐轭瀛翦篝骈祠弪蟓骈戾箝彐矧磲吼狎孱骈祠弪ㄩ篝蜷铉⒈鞍蛮翦簏碑肼碑吐碑锹碑月卑蛮翦簏卑鞍蛮翦簏狗懂碎曼沟钞烷曼钩碑情垄ㄩ姝翦篝桢祓弪ㄣ镱汜翦钺翦篝蜷铉Ⅺ卑包骈戾箝彐矧磲Ⅺ卑鞍骈戾箝彐矧磲Ⅺ卑鞍鞍包骈戾箝彐矧磲Ⅺ卑鞍鞍鞍鞍骈戾箝彐矧磲Ⅺ卑鞍鞍鞍鞍鞍包骈戾箝彐矧磲Ⅺ卑包骈戾箝彐矧磲舁趄蹂Ⅺ卑鞍骈戾箝彐矧磲舁趄蹂Ⅺ卑鞍鞍包骈戾箝彐矧磲舁趄蹂Ⅺ卑鞍鞍鞍鞍骈戾箝彐矧磲舁趄蹂Ⅺ卑鞍鞍鞍鞍鞍包骈戾箝彐矧磲舁趄蹂┅ㄩ篝蜷铉⒊鞍蛮翦簏钞肼钞吐钞锹钞月嘲蛮翦簏伯碎曼伯烷垄ㄩ姝翦篝桢祓弪ㄣ镱汜翦钺翦篝蜷铉Ⅺ嘲包骈戾箝彐矧磲Ⅺ嘲鞍骈戾箝彐矧磲Ⅺ嘲鞍鞍包骈戾箝彐矧磲Ⅺ嘲鞍鞍鞍鞍骈戾箝彐矧磲Ⅺ嘲鞍鞍鞍鞍鞍包骈戾箝彐矧磲Ⅺ嘲包骈戾箝彐矧磲舁趄蹂Ⅺ嘲鞍骈戾箝彐矧磲舁趄蹂Ⅺ嘲鞍鞍包骈戾箝彐矧磲舁趄蹂┅┅ㄤ彐轭瀛翦篝骈祠弪蟓骈蝮吼狎孱骈祠弪ㄩ篝蜷铉阿ㄩ姝翦篝桢祓弪Ⅺ骘稂骈蝮扉篝ф镲ㄡ戾犷潋獒洪雉卑┅┅ㄤ彐轭瀛翦篝骈祠弪蟓骒镝吼狎孱骈祠弪ㄩ篝蜷铉⒋伯阿ㄩ姝翦篝桢祓弪Ⅺ鲠祯妩骒镝扉篝鲠祯⒋并┅ㄩ篝蜷铉爱阿ㄩ姝翦篝桢祓弪Ⅺ鲠祯妩骒镝扉篝鲠祯⑨忏┅ㄩ篝蜷铉⒊伯巢ㄩ姝翦篝桢祓弪Ⅺ鲠祯妩骒镝扉篝鲠祯⒊伯巢┅┅ㄤ彐轭瀛翦篝骈祠弪蟓骒镝舡溴驷蹯吼狎孱骈祠弪ㄩ篝蜷铉⒈阿ㄩ姝翦篝桢祓弪Ⅺ鲠祯妩骒镝舁碑癌扉篝鲠祯⑨忏┅┅ㄤ彐轭瀛翦篝骈祠弪蟓骘蝽狒粕赝翳轶轶锰浩弦土吼狎孱骈祠弪ㄩ篝蜷铉⑨猗ㄩ姝翦篝桢祓弪Ⅺ咙璃骘蝽狒ě岌рЗ┅ㄤ彐轭瀛翦篝骈祠弪蟓轭溴铘眭祠殪轭瀛翦眇灬翦吼狎孱骈祠弪ㄩ篝蜷铉？④骘忉蜍堍忉堍茴ㄩ姝翦篝桢祓弪Ⅺ骘稂轭溴铘鏖漪杞博扉篝ф镲？④铈镲忉蜍钴⑩狷堍茴┅ㄩ篝蜷铉？茴骘忉蜍堍忉堍茴ㄩ姝翦篝桢祓弪Ⅺ骘稂轭溴铘鏖漪杞铂骈蝮艚趄蹂扉篝ф镲？④铈镲忉蜍钴⑩狷堍茴┅ㄩ篝蜷铉？④骘忉蜍堍忉堍茴ㄩ姝翦篝桢祓弪Ⅺ骘稂轭溴铘鏖漪杞铂忪犷虢趄蹂扉篝ф镲？④铈镲忉蜍钴⑩狷堍茴┅ㄩ篝蜷铉？茴骘忉蜍堍忉堍茴ㄩ姝翦篝桢祓弪Ⅺ骘稂轭溴铘鏖漪杞铂忪犷虢趄蹂骈蝮艚趄蹂扉篝ф镲？④铈镲忉蜍钴⑩狷堍茴┅┅ㄤ彐轭瀛翦篝骈祠弪蟓轭溴铘吼狎孱骈祠弪ㄩ篝蜷铉㈥轭赆ㄩ姝翦篝桢祓弪Ⅺш轭赆轭溴铘┅ㄩ篝蜷铉觊铌幄ㄩ姝翦篝桢祓弪Ⅺш轭赆轭溴铘ㄦ轵篝紧蝓濠┅ㄩ篝蜷铉㈥轭赆ㄩ姝翦篝桢祓弪Ⅺш轭赆轭溴铘ㄢ灬铍紧蝓濠┅ㄣ飙轭翦蝠镬轰轶徕戾轭翦蝠镬簌铘狲换怩殪糸铙ㄤ彐轭瀛翦篝怩殪糸铙